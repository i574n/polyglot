#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[]}}

#!markdown

# am'

#!spiral

// // test

open testing

prototype append t : t -> t -> t

#!spiral

open rust_operators

#!spiral

inl types () =
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"[$0]\")>] type Slice<'T> = class end"
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"[$0; $1]\")>] type Slice'<'T, 'U> = class end"
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"Vec<$0>\")>] type Vec<'T> = class end"

#!spiral

nominal slice t = $"Slice<`t>"
nominal slice' t u = $"Slice'<`t, `u>"
nominal vec t = $"Vec<`t>"

#!markdown

## append

#!spiral

instance append a dim {int; number} t =
    am.append

#!markdown

## /@

#!spiral

inl (/@) a b =
    b |> append a

#!spiral

// // test

a ;[ "a"; "b" ] /@ a ;[ "c"; "d" ]
|> _assert_eq (a ;[ "a"; "b"; "c"; "d" ] : _ i32 _)

#!markdown

## collect

#!spiral

inl collect forall t r. (fn : t -> a i32 r) (items : a i32 t) : a i32 r =
    items
    |> am.map fn
    |> am.fold (/@) (a ;[])

#!markdown

## choose

#!spiral

inl choose f l =
    (l, [])
    ||> am.foldBack fun x acc =>
        match f x with
        | Some y => y :: acc
        | None => acc
    |> listm.toArray

#!spiral

// // test

(am.init 10i32 id : a _ _)
|> choose (fun x => if x % 2 = 0 then Some x else None)
|> _assert_eq (a ;[ 0; 2; 4; 6; 8 ] : _ i32 _)

#!markdown

## sum

#!spiral

inl sum (a' : a _ _) =
    a' |> am.fold (+) 0

#!spiral

// // test

am.init 10i32 id
|> sum
|> _assert_eq 45

#!markdown

## init_series

#!spiral

inl init_series start end inc =
    inl total = conv ((end - start) / inc) + 1
    am.init total (conv >> (*) inc >> (+) start) : a i32 _

#!spiral

// // test

init_series 0 1 0.5
|> _assert_eq (a ;[0f64; 0.5; 1])

#!markdown

## head

#!spiral

inl head (ar : a _ _) =
    if var_is ar || length ar > 0
    then index ar 0
    else error_type "The length of the array should be greater than 0."

#!markdown

## last

#!spiral

inl last (ar : a _ _) =
    inl len = length ar
    if var_is ar || len > 0
    then index ar (len - 1)
    else error_type "The length of the array should be greater than 0."

#!spiral

// // test

am.init 10i32 id
|> last
|> _assert_eq 9

#!markdown

## try_pick

#!spiral

inl try_pick forall t u. (fn : t -> option u) (array : a _ t) : option u =
    (array, None)
    ||> am.foldBack fun x acc =>
        match acc with
        | Some _ => acc
        | None => x |> fn

#!spiral

// // test

am.init 10i32 id
|> try_pick (fun x => if x = 5i32 then Some x else None)
|> _assert_eq (Some 5i32)

#!markdown

## indexed'

#!spiral

inl indexed' forall t dim {int; number} dim' {int; number} u {number}. (ar : a dim t) : a dim' (u * t) =
    ((0, a ;[]), ar)
    ||> am.fold fun (i, acc) x =>
        i + 1, acc /@ a ;[i, x]
    |> snd

#!spiral

// // test

am.init 3i32 ((*) 2)
|> indexed'
|> _assert_eq (a ;[0i32, 0; 1, 2; 2, 4] : a i32 _)

#!markdown

## map_base

#!spiral

inl map_base forall t u. (fn : t -> u) (x : array_base t) : array_base u =
    a x
    |> am.map fn
    |> fun (a x : _ i64 _) => x

#!spiral

inl average forall el {number}. (array : a _ el) : el =
    $"!array |> Array.average"

inl length forall dim el {number}. (array : a dim el) : dim =
    $"!array |> Array.length"

inl parallel_map forall dim el el'. (fn : el -> el') (array : a dim el) : a dim el' =
    $"!array |> Array.Parallel.map !fn"

inl sort_by forall dim el. (fn : el -> _) (array : a dim el) : a dim el =
    $"!array |> Array.sortBy !fn"

inl sort_descending forall dim el. (array : a dim el) : a dim el =
    $"!array |> Array.sortDescending"

inl transpose forall dim el. (array : a dim (a dim el)) : a dim (a dim el) =
    $"!array |> Array.transpose"

inl try_item forall dim el. (i : i32) (array : a dim el) : option el =
    $"!array |> Array.tryItem !i" |> optionm'.unbox

#!markdown

## indexed

#!spiral

inl indexed (array : a _ _) =
    (([], 0), array)
    ||> am.fold fun (acc, i) x =>
        (i, x) :: acc, i + 1
    |> fst
    |> listm.rev
    |> listm.toArray

#!spiral

// // test

am.init 3i32 ((*) 2)
|> indexed
|> _assert_eq (a ;[0i32, 0; 1, 2; 2, 4] : a i32 _)

#!markdown

## from_vec

#!spiral

inl from_vec forall dim el. (vec : vec el) : a dim el =
    inl vec = join vec
    !\($'"fable_library_rust::NativeArray_::array_from(!vec)"')

#!markdown

## to_vec

#!spiral

inl to_vec forall t. (ar : array_base t) : vec t =
    inl ar = join ar
    !\($'"!ar.to_vec()"')

#!markdown

## map_vec

#!spiral

inl map_vec forall dim t u. (fn : t -> u) (ar : a dim t) : a dim u =
    inl fn = join fn
    inl (a ar) = ar
    inl ar = ar |> to_vec
    !\($'"!ar.iter().map(|x| !fn(x.clone())).collect()"')
    |> from_vec

#!markdown

## mapi_vec

#!spiral

inl mapi_vec forall dim t u. (fn : dim -> t -> u) (ar : a dim t) : a dim u =
    inl fn = join fn
    inl (a ar) = ar
    inl ar = ar |> to_vec
    !\($'"!ar.iter().enumerate().map(|(i, x)| !fn(i.try_into().unwrap())(x.clone())).collect()"')
    |> from_vec

#!markdown

## filter_vec

#!spiral

inl filter_vec forall dim t. (fn : t -> bool) (ar : a dim t) : a dim t =
    inl fn = join fn
    inl (a ar) = ar
    inl ar = ar |> to_vec
    !\($'"!ar.into_iter().filter(|x| !fn(x.clone().clone())).collect()"')
    |> from_vec

#!markdown

## vec_push

#!spiral

inl vec_push forall el. (el : el) (vec : vec el) : vec el =
    inl el = join el
    inl vec = join vec
    !\($'"let mut !vec = !vec"')
    // inl vec = vec |> rust.to_mut
    !\($'"!vec.push(!el)"')
    !\($'"!vec"')

#!markdown

## vec_reverse

#!spiral

inl vec_reverse forall el. (vec : vec el) : vec el =
    inl vec = join vec
    !\($'"let mut !vec = !vec"')
    !\($'"!vec.reverse()"')
    !\($'"!vec"')

#!markdown

## vec_retain

#!spiral

inl vec_retain forall el. (fn : el -> bool) (vec : vec el) : vec el =
    inl vec = join vec
    inl fn = join fn
    !\($'"let mut !vec = !vec"')
    // inl vec = vec |> rust.to_mut
    !\($'"!vec.retain(|x| !fn(x.clone()))"')
    !\($'"!vec"')

#!markdown

## vec_sort_by_key

#!spiral

inl vec_sort_by_key forall el t. (fn : el -> t) (vec : vec el) : vec el =
    inl vec = join vec
    inl fn = join fn
    !\($'"let mut !vec = !vec"')
    // inl vec = vec |> rust.to_mut
    !\($'"!vec.sort_by_key(|x| !fn(x.clone()))"')
    !\($'"!vec"')

#!markdown

## vec_extend

#!spiral

inl vec_extend forall el. (el : vec el) (vec : vec el) : vec el =
    inl el = join el
    inl vec = join vec
    !\($'"let mut !vec = !vec"')
    // inl vec = vec |> rust.to_mut
    !\($'"!vec.extend(!el)"')
    !\($'"!vec"')

#!markdown

## vec_collect

#!spiral

inl vec_collect fn vec =
    ((;[] |> to_vec), (vec |> from_vec : _ i32 _))
    ||> am.fold fun acc x =>
        acc |> vec_extend (fn x)

#!markdown

## vec_collect_option

#!spiral

inl vec_collect_option vec =
    ((;[] |> to_vec |> Ok), (vec |> from_vec : _ i32 _))
    ||> am.fold fun acc x =>
        x
        |> resultm.unbox
        |> fun x =>
            match acc, x |> resultm.map optionm'.unbox with
            | Ok acc, Ok (Some x) => acc |> vec_extend x |> Ok
            | _, Error error => error |> Error
            | _ => acc

#!markdown

## vec_collect_into

#!spiral

inl vec_collect_into forall (c : * -> * -> *) t e.
    (x : vec (c t e))
    : c (vec t) e
    =
    !\($'"!x.into_iter().collect()"')

#!markdown

## vec_mapi

#!spiral

inl vec_mapi forall dim t u. (fn : dim -> t -> u) (ar : vec t) : vec u =
    inl fn = join fn
    inl ar = join ar
    !\($'"!ar.iter().enumerate().map(|(i, x)| !fn(i.try_into().unwrap())(x.clone())).collect()"')

#!markdown

## vec_map

#!spiral

inl vec_map forall t u. (fn : t -> u) (ar : vec t) : vec u =
    inl fn = join fn
    inl ar = join ar
    !\($'"!ar.iter().map(|x| !fn(x.clone())).collect()"')

#!markdown

## enumerate

#!spiral

inl enumerate forall dim {int; number} t. (ar : a dim t) : a dim (unativeint * t) =
    inl (a ar) = ar
    inl ar = ar |> to_vec
    !\($'"!ar.into_iter().enumerate().map(std::rc::Rc::new).collect()"')
    |> from_vec
    |> map_vec from_pair

#!markdown

## slice'

#!spiral

inl slice' forall dim el. (x : option el) : slice' el dim =
    match x with
    | Some x => !\($'"[!x]"')
    | None =>
        !\($'"[\\\"\\\".to_string()]"') : slice' el dim
            // emit_expr `(()) `(slice' el dim) () ($'"[@dim]"' : string) : slice' el 10
            // !\( : string) : slice' el i32 // !\($'"[]"')

#!markdown

## as_slice

#!spiral

inl as_slice forall t. (x : array_base t) : rust.ref' (slice t) =
    inl x = x |> to_vec
    !\($'"!x.as_slice()"')
