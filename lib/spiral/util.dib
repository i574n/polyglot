#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[]}}

#!markdown

# util

#!spiral

// // test

open testing

#!markdown

## ski

#!spiral

union rec ski =
    | I
    | K
    | S
    | App : ski * ski

inl rec eval ski =
    match ski with
    | App (App (K, x), y) => eval x
    | App (App (App (S, x), y), z) => eval (App (App (x, z), App (y, z)))
    | App (I, x) => eval x
    | App (K, x) => App (K, eval x)
    | App (f, x) => eval (App (eval f, x))
    | _ => ski

#!spiral

// // test

eval I
|> _assert_eq I

eval (App (I, I))
|> _assert_eq I

eval (App (I, App (I, I)))
|> _assert_eq I

eval (App (App (I, I), I))
|> _assert_eq I

eval (App (App (App (I, I), I), I))
|> _assert_eq I

eval K
|> _assert_eq K

eval (App (K, I))
|> _assert_eq (App (K, I))

eval (App (K, K))
|> _assert_eq (App (K, K))

eval (App (App (K, I), K))
|> _assert_eq I

eval (App (App (K, K), I))
|> _assert_eq K

eval (App (App (App (App (K, K), I), S), K))
|> _assert_eq S

eval S
|> _assert_eq S

eval (App (App (App (S, I), I), I))
|> _assert_eq I

eval (App (App (App (S, K), K), I))
|> _assert_eq I

eval (App (App (App (S, K), I), (App (App (K, I), S))))
|> _assert_eq I

eval (App (App (K, S), App (I, App (App (App (S, K), S), I))))
|> _assert_eq S

eval (App (App (App (S, K), I), K))
|> _assert_eq K

#!markdown

## sm'.concat_array_trailing

#!spiral

inl types () =
    rust.types ()
    sm'.types ()

#!spiral

// // test
// // rust=

;[
    "1"
    "2"
    "3"
]
|> fun x =>
    inl code = (a x : _ i32 _) |> sm'.concat_array_trailing "\n"
    code
    |> _assert_eq "1\n2\n3\n"

#!markdown

## sm'.concat_list_trailing

#!spiral

// // test
// // rust=

[
    "1"
    "2"
    "3"
]
|> fun x =>
    inl code = (x : _) |> sm'.concat_list_trailing "\n"
    code
    |> _assert_eq "1\n2\n3\n"

#!markdown

## sm'.concat_list_heap_trailing

#!spiral

// // test
// // rust=

types ()

[
    "1"
    "2"
    "3"
]
|> fun x =>
    inl code = (x : _) |> sm'.concat_list_heap_trailing "\n"
    code
    |> _assert_eq "1\n2\n3\n"
