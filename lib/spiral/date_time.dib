#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[]}}

#!markdown

# date_time

#!spiral

open rust_operators
open sm'_operators

#!spiral

// // test

open testing

#!markdown

## types

#!spiral

inl types () =
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"chrono::DateTime<$0>\")>] type chrono_DateTime<'T> = class end"
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"chrono::Local\")>] type chrono_Local = class end"
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"chrono::NaiveDateTime\")>] type chrono_NaiveDateTime = class end"
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"chrono::Utc\")>] type chrono_Utc = class end"
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"std::time::Duration\")>] type std_time_Duration = class end"

#!spiral

nominal timestamp = i64
nominal duration = $"std_time_Duration"
nominal date_time = $"System.DateTime"
nominal date_time' t = $"chrono_DateTime<`t>"
nominal local = $"chrono_Local"
nominal naive_date_time = $"chrono_NaiveDateTime"
nominal utc = $"chrono_Utc"

#!markdown

## naive_utc

#!spiral

inl naive_utc (date_time : date_time' utc) : naive_date_time =
    inl date_time = join date_time
    !\($'"!date_time.naive_utc()"')

#!markdown

## to_local

#!spiral

inl to_local (date_time : date_time' utc) : date_time' local =
    inl naive_date_time = date_time |> naive_utc
    !\($'"chrono::offset::TimeZone::from_utc_datetime(&chrono::Local, &!naive_date_time)"')

#!markdown

## from_timestamp'

#!spiral

inl from_timestamp' forall t {number; int}. (timestamp : t) : option (date_time' utc) =
    inl timestamp = join timestamp
    inl result : optionm'.option' (date_time' utc) =
        !\($'"chrono::DateTime::from_timestamp_micros(!timestamp / 1000i64)"')
    result |> optionm'.unbox

#!markdown

## ticks

#!spiral

inl ticks (date_time : date_time) : timestamp =
    date_time |> $'_.Ticks'

#!markdown

## format

#!spiral

inl format (format : string) (date_time : date_time) : string =
    $'!date_time.ToString' format

#!markdown

## format_iso8601

#!spiral

inl format_iso8601 (date_time : date_time) =
    date_time |> format "yyyy-MM-ddTHH-mm-ss.fff"

#!markdown

## format'

#!spiral

inl format' (format : string) (date_time : date_time' utc) : sm'.std_string =
    inl format = #format
    inl date_time = join date_time
    !\($'"!date_time.format(!format).to_string()"')

#!markdown

## format''

#!spiral

inl format'' (format : string) (date_time : date_time' _) : sm'.std_string =
    inl format = #format
    inl date_time = join date_time
    !\($'"!date_time.format(!format).to_string()"')

#!markdown

## format_timestamp

#!spiral

inl format_timestamp (timestamp : sm'.std_string) =
    inl timestamp = join timestamp
    timestamp
    |> fun x => !\($'"!x.parse().unwrap()"') : i64
    |> from_timestamp'
    |> optionm.map fun x =>
        x
        |> to_local
        |> format'' "%Y-%m-%d %H:%M:%S"
        |> sm'.from_std_string
    |> resultm.from_option

#!markdown

## duration_from_millis

#!spiral

inl duration_from_millis (ms : u64) : duration =
    inl ms = join ms
    !\($'"std::time::Duration::from_millis(!ms)"')

#!markdown

## get_environment_variable

#!spiral

inl get_environment_variable (var : string) : string =
    $"System.Environment.GetEnvironmentVariable !var"

#!spiral

// // test

inl test_guid () =
    guid.new_guid "FEDCBA98-7654-3210-FEDC-BA9876543210"

#!spiral

type timestamp_guid = guid.guid
type date_time_guid = guid.guid

#!markdown

## date_time_guid_from_date_time

#!spiral

inl date_time_guid_from_date_time (guid : guid.guid) (date_time : date_time) =
    inl guid = guid |> sm'.obj_to_string
    inl prefix = $'!date_time.ToString "yyyyMMdd-HHmm-ssff-ffff-f"' : string
    $'`date_time_guid $"{!prefix}{!guid.[!prefix.Length..]}"' : date_time_guid

#!spiral

// // test

date_time_guid_from_date_time (test_guid ()) $'System.DateTime.MinValue'
|> sm'.obj_to_string
|> _assert_eq' "00010101-0000-0000-0000-0a9876543210"

#!spiral

// // test

date_time_guid_from_date_time (test_guid ()) $'System.DateTime.MaxValue'
|> sm'.obj_to_string
|> _assert_eq $'$"99991231-2359-5999-9999-9{(!test_guid() |> string).[^10..]}"'

#!spiral

// // test

date_time_guid_from_date_time (test_guid ()) $'System.DateTime.UnixEpoch'
|> sm'.obj_to_string
|> _assert_eq $'$"19700101-0000-0000-0000-0{(!test_guid () |> string).[^10..]}"'

#!markdown

## date_time_from_guid

#!spiral

inl date_time_from_guid (date_time_guid : date_time_guid) =
    inl date_time_guid = date_time_guid |> sm'.obj_to_string
    inl sm'_replace = join sm'.replace
    run_target function
        | Rust _ => fun () =>
            $'System.DateTime.Parse (!date_time_guid.[..24] |> !sm'_replace "-" "")' : date_time
        | _ => fun () => $'System.DateTime.ParseExact (!date_time_guid.[..24] |> !sm'_replace "-" "", "yyyyMMddHHmmssfffffff", null)' : date_time

#!spiral

// // test

date_time_from_guid (guid.new_guid "00010101-0000-0000-0000-0a9876543210")
|> _assert_eq' $'System.DateTime.MinValue'

#!spiral

// // test

date_time_from_guid (guid.new_guid $'$"99991231-2359-5999-9999-9{(!test_guid () |> string).[^10..]}"')
|> _assert_eq' $'System.DateTime.MaxValue'

#!spiral

// // test

date_time_from_guid (guid.new_guid $'$"19700101-0000-0000-0000-0{(!test_guid () |> string).[^10..]}"')
|> _assert_eq' $'System.DateTime.UnixEpoch'

#!markdown

## timestamp_guid_from_timestamp

#!spiral

inl timestamp_guid_from_timestamp (guid : guid.guid) (timestamp : timestamp) : timestamp_guid =
    inl guid = guid |> sm'.obj_to_string
    inl timestamp = timestamp |> sm'.obj_to_string |> sm'.pad_left 18i32 '0'
    $'`timestamp_guid $"{!timestamp.[0..7]}-{!timestamp.[8..11]}-{!timestamp.[12..15]}-{!timestamp.[16..17]}{!guid.[21..]}"'

#!spiral

// // test

timestamp_guid_from_timestamp (test_guid ()) (timestamp 0i64)
|> _assert_eq' (guid.new_guid "00000000-0000-0000-00dc-ba9876543210")

#!spiral

// // test

timestamp_guid_from_timestamp (test_guid ()) (timestamp 999999999999999999i64)
|> _assert_eq' (guid.new_guid $'$"99999999-9999-9999-99dc-b{(!test_guid () |> string).[^10..]}"')

#!markdown

## timestamp_from_guid

#!spiral

inl timestamp_from_guid (guid : date_time_guid) : timestamp =
    inl guid = guid |> sm'.obj_to_string
    $'`i64 $"{!guid.[0..7]}{!guid.[9..12]}{!guid.[14..17]}{!guid.[19..20]}"'

#!spiral

// // test

timestamp_from_guid (guid.new_guid "00000000-0000-0000-00dc-ba9876543210")
|> _assert_eq (timestamp 0)

#!spiral

// // test

timestamp_from_guid (guid.new_guid $'$"99999999-9999-9999-99{(!test_guid () |> string).[^14..]}"')
|> _assert_eq (timestamp 999999999999999999)

#!markdown

## now

#!spiral

inl now () : date_time =
    $'System.DateTime.Now'

#!markdown

## utc_now

#!spiral

inl utc_now () : date_time =
    $'System.DateTime.UtcNow'

#!markdown

## time_span

#!spiral

nominal time_span = $'System.TimeSpan'

#!markdown

## new_time_span

#!spiral

inl new_time_span (a : date_time) (b : date_time) : time_span =
    $'!b - !a '

#!markdown

## total_seconds

#!spiral

inl total_seconds (time_span : time_span) : f64 =
    time_span |> $'_.TotalSeconds'

#!markdown

## new_guid_from_date_time

#!spiral

inl new_guid_from_date_time (date_time : date_time) =
    inl guid = guid.new_raw_guid ()
    date_time_guid_from_date_time guid date_time

#!spiral

// // test

utc_now ()
|> new_guid_from_date_time
|> date_time_from_guid
|> fun date_time => new_time_span date_time (utc_now ()) |> total_seconds |> i32
|> _assert_eq 0

#!markdown

## new_guid_from_timestamp

#!spiral

inl new_guid_from_timestamp (timestamp : timestamp) =
    inl guid = guid.new_raw_guid ()
    timestamp_guid_from_timestamp guid timestamp

#!spiral

// // test

utc_now ()
|> ticks
|> new_guid_from_timestamp
|> timestamp_from_guid
|> fun (timestamp timestamp) => (timestamp - (utc_now () |> ticks |> fun (timestamp x) => x)) / 100000i64
|> _assert_eq 0i64

#!fsharp

//// test

type DateTimeWithZone = {
    DateTime: System.DateTimeOffset
    TimeZone: System.TimeZoneInfo
}

try
    let now = System.DateTimeOffset.Now
    let timeZoneInfo = System.TimeZoneInfo.Local
    let dateTimeWithZone = { DateTime = now; TimeZone = timeZoneInfo }

    printfn "DateTime: %O" dateTimeWithZone.DateTime
    printfn "Time Zone: %s" dateTimeWithZone.TimeZone.DisplayName
    printfn "Is DST: %b" (timeZoneInfo.IsDaylightSavingTime(dateTimeWithZone.DateTime.DateTime))
    printfn "v1: %s" (dateTimeWithZone.TimeZone.GetUtcOffset(now) |> string)
    printfn "v2: %s" (dateTimeWithZone.TimeZone |> string)
with ex ->
    printfn "error: %A" ex

#!markdown

## main

#!spiral

inl main () =
    $"let date_time_guid_from_date_time x = !date_time_guid_from_date_time x" : ()
    $"let date_time_from_guid x = !date_time_from_guid x" : ()
    $"let timestamp_guid_from_timestamp x = !timestamp_guid_from_timestamp x" : ()
    $"let timestamp_from_guid x = !timestamp_from_guid x" : ()
    $"let new_guid_from_date_time x = !new_guid_from_date_time x" : ()
    $"let new_guid_from_timestamp x = !new_guid_from_timestamp x" : ()
    $"let format x = !format x" : ()
    $"let format_iso8601 x = !format_iso8601 x" : ()
