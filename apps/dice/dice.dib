#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[]}}

#!markdown

# dice (Polyglot)

#!spiral

// // test

open testing

#!markdown

## sixth_power_sequence

#!spiral

inl sixth_power_sequence () =
    stream.iterate ((*) 6) 1

#!spiral

// // test

sixth_power_sequence ()
|> stream.take_while (fun _ i => i <= 7i32)
|> stream.to_list
|> _equal [ 1i32; 6; 36; 216; 1296; 7776; 46656; 279936 ]

#!markdown

## accumulate_dice_rolls

#!spiral

inl accumulate_dice_rolls
    (log : option (string -> ()))
    (rolls : list u8)
    (power : i8)
    acc
    =
    inl rec body acc power rolls =
        match rolls with
        | _ when power < 0 =>
            log |> optionm'.iter ((|>) $"$\"accumulate_dice_rolls / power: {!power} / acc: {!acc}\"")
            Some (acc + 1, rolls)
        | [] => None
        | roll :: rest when roll > 1 =>
            inl coeff = sixth_power_sequence () |> stream.item power
            inl value = conv (roll - 1) * coeff
            log |> optionm'.iter ((|>) $"$\"accumulate_dice_rolls / power: {!power} / acc: {!acc} / roll: {!roll} / value: {!value}\""
            )
            loop (acc + value) (power - 1) rest
        | roll :: rest =>
            log |> optionm'.iter ((|>) $"$\"accumulate_dice_rolls / power: {!power} / acc: {!acc} / roll: {!roll}\"")
            loop acc (power - 1) rest
    and inl loop acc power rolls =
        if var_is rolls |> not
        then body acc power rolls
        else
            inl acc = dyn acc
            join body acc power rolls
    loop acc power rolls

#!spiral

// // test

accumulate_dice_rolls (Some console.write_line) [ 6; 5; 4; 3; 2 ] 0 1000i32
|> _equal (Some (1006, [ 5; 4; 3; 2 ]))

#!spiral

// // test

accumulate_dice_rolls (Some console.write_line) [ 6; 5; 4; 3; 2 ] 1 1000i32
|> _equal (Some (1035, [ 4; 3; 2 ]))

#!spiral

// // test

accumulate_dice_rolls (Some console.write_line) [ 6; 5; 4; 3; 2 ] 2 1000i32
|> _equal (Some (1208, [ 3; 2 ]))

#!markdown

## roll_within_bounds

#!spiral

inl roll_within_bounds (log : option (string -> ())) max rolls =
    inl power = listm.length rolls - 1
    match accumulate_dice_rolls log rolls power 0 with
    | Some (result, _) when result >= 1 && result <= max => Some result
    | _ => None

#!spiral

// // test

roll_within_bounds (Some console.write_line) 2000i32 [ 1; 5; 4; 4; 5 ]
|> _equal (Some 995)

#!spiral

// // test

roll_within_bounds (Some console.write_line) 2000i32 [ 2; 2; 6; 4; 5 ]
|> _equal (Some 1715)

#!spiral

// // test

roll_within_bounds (Some console.write_line) 2000i32 [ 4; 1; 1; 2; 3 ]
|> _equal None

#!markdown

## calculate_dice_count

#!spiral

inl calculate_dice_count (log : option (string -> ())) max =
    inl rec body n p =
        inl return () =
            log |> optionm'.iter ((|>) $"$\"calculate_dice_count / max: {!max} / n: {!n} / p: {!p}\"")
            n

        if p < max then
            inl p' = p * 6
            if p' > p
            then loop (n + 1) p'
            else return ()
        else return ()

    and inl loop n p =
        if var_is max |> not
        then body n p
        else
            inl n = dyn n
            inl p = dyn p
            join body n p
    if max = 1
    then 1
    else loop 0 1

#!spiral

// // test

calculate_dice_count (Some console.write_line) 36i32
|> _equal 2i32

#!spiral

// // test

calculate_dice_count (Some console.write_line) 7777i32
|> _equal 6i32

#!markdown

## roll_dice

#!spiral

inl roll_dice () : u8 =
    run_target function
        | Fsharp => fun () => $"System.Random().Next (1, 7) |> uint8"
        | Rust (Native) => fun () => rust.emit_expr () "rand::Rng::gen_range(&mut rand::thread_rng(), 1..7)"
        | Rust (Wasm) => fun () => rust.emit_expr () "1"

#!markdown

## rotate_number

#!spiral

inl rotate_number forall t {number}. (max : i64) (n : t) : t =
    (conv n - 1 + max) % max + 1 |> conv

#!markdown

## rotate_numbers

#!spiral

inl rotate_numbers max items =
    items |> stream.map (rotate_number max)

#!spiral

// // test

listm'.init_series -1 14 1i32
|> stream.from_list
|> rotate_numbers 6
|> stream.to_list
|> _equal [ 5; 6; 1; 2; 3; 4; 5; 6; 1; 2; 3; 4; 5; 6; 1; 2 ]

#!markdown

## create_sequential_roller

#!spiral

inl create_sequential_roller (log : option (string -> ())) list =
    log |> optionm'.iter ((|>) $"$\"create_sequential_roller ()\"")
    inl s = list |> listm.rev |> listm.append list |> stream.from_list |> stream.memoize
    inl current_index = mut 0i64
    inl acc = mut 1
    inl len = mut -1
    inl last_item = mut None
    let rec loop () =
        fun () =>
            inl current_index = *current_index
            inl acc = *acc
            inl len = *len
            inl last_item = *last_item
            log |> optionm'.iter ((|>) $"$\"create_sequential_roller / roll / current_index: {!current_index} / acc: {!acc} / len: {!len} / last_item: %A{!last_item}\"")
        |> fun x => x ()
        match s () |> stream.try_item *current_index with
        | Some item =>
            current_index <- *current_index + 1
            last_item <- Some item
            item
        | None =>
            log |> optionm'.iter ((|>) $"$\"create_sequential_roller / roll / None\"")
            if *len = -1
            then len <- *current_index
            acc <-
                if *acc >= *len
                then 1
                else *acc + 1
            current_index <- *acc - 1
            last_item <- None
            loop ()
    loop

#!spiral

// // test

inl sequential_roll = create_sequential_roller (Some console.write_line) [ 1i32; 2; 3; 4 ]

am.init 50i32 (ignore >> sequential_roll)
|> _equal (a ;[ 1; 2; 3; 4; 4; 3; 2; 1; 2; 3; 4; 4; 3; 2; 1; 3; 4; 4; 3; 2; 1; 4; 4; 3; 2; 1;
4; 3; 2; 1; 3; 2; 1; 2; 1; 1; 1; 2; 3; 4; 4; 3; 2; 1; 2; 3; 4; 4; 3; 2 ] : a i32 i32)

#!markdown

## roll_progressively

#!spiral

inl roll_progressively (log : option (string -> ())) roll reroll max =
    inl power = (calculate_dice_count log max) - 1
    let rec loop rolls size =
        if size < power + 1
        then loop (roll () :: rolls) (size + 1)
        else accumulate_dice_rolls log rolls power 0 |> function
            | Some (result, _) when result <= max => result
            | _ when reroll => loop (listm.init power (fun _ => roll ())) power
            | _ => loop (roll () :: rolls) (size + 1)
    loop [] 0

#!spiral

// // test

roll_progressively None roll_dice false 1
|> _equal 1i32

#!spiral

// // test

inl sequential_roll = create_sequential_roller (Some console.write_line) [ 5; 4; 4; 5; 1 ]

roll_progressively (Some console.write_line) sequential_roll false 2000i32
|> _equal 995

#!spiral

// // test

inl sequential_roll = create_sequential_roller (Some console.write_line) [ 5; 4; 4; 5; 2 ]

roll_progressively (Some console.write_line) sequential_roll false 2000i32
|> _equal 1678

#!spiral

// // test

join dyn listm'.init_series 1i32 10000 1
|> listm.map (fun _ => roll_progressively None roll_dice false 10i32)
|> listm'.group_by id
|> listm.map (fun (k, v) => k, (listm.length v : i32))
|> listm.toArray
|> fun (ar : a i32 _) =>
    $"!ar |> Array.sortBy (fun (struct (a, b)) -> a)" : a i32 $"struct (int32 * int32)"

#!spiral

// // test
// // print_code=false

join dyn listm'.init_series 1i32 10000 1
|> listm.map (fun _ => roll_progressively None roll_dice true 10i32)
|> listm'.group_by id
|> listm.map (fun (k, v) => k, (listm.length v : i32))
|> listm.toArray
|> fun (ar : a i32 _) =>
    $"!ar |> Array.sortBy (fun (struct (a, b)) -> a)" : a i32 $"struct (int32 * int32)"

#!spiral

// // test
// // timeout=60000

join dyn listm'.init_series 1i32 100 1
|> listm.iter fun n =>
    listm'.init_series 0i32 1 1
    |> listm.iter fun reroll =>
        join dyn listm'.init_series 1i32 3500 1
        |> listm.map fun _ => roll_progressively None roll_dice (reroll = 1) n
        |> listm'.group_by id
        |> listm.length
        |> _equal n

#!markdown

## main

#!spiral

inl main (_args : array_base string) =
    inl result = roll_progressively (Some console.write_line) roll_dice true (limit.max : i64)
    console.write_line ($"$\"result: {!result}\"" : string)
    0i32

inl main () =
    inl rotate_numbers' : i64 -> stream.stream u8 -> stream.stream u8 =
        rotate_numbers

    inl create_sequential_roller' :
        optionm'.option' (string -> ())
        -> list u8 
        -> (() -> u8)
        =
        optionm'.unbox >> create_sequential_roller

    inl roll_progressively' :
        optionm'.option' (string -> ()) 
        -> (() -> u8)
        -> bool
        -> u64
        -> u64
        =
        optionm'.unbox >> roll_progressively

    inl roll_within_bounds' :
        optionm'.option' (string -> ())
        -> u64
        -> list u8
        -> optionm'.option' u64
        =
        fun a b c =>
            (optionm'.unbox >> roll_within_bounds) a b c
            |> optionm'.box

    $"let rotate_numbers x = !rotate_numbers' x" : ()
    $"let create_sequential_roller x = !create_sequential_roller' x" : ()
    $"let roll_progressively x = !roll_progressively' x" : ()
    $"let roll_within_bounds x = !roll_within_bounds' x" : ()
    $"let main args = !main args" : ()
